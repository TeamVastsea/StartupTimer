buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.matthewprenger.cursegradle"

group = 'org.embeddedt'
version = '1.0.1'
archivesBaseName = 'startuptimer'

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.cleanroommc.com"
    }
}

dependencies {
    deobfCompile ("zone.rong:mixinbooter:7.0")
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.startuptimer.refmap.json"
    }
}

jar {
    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": true,
                "FMLCorePlugin": 'org.embeddedt.startuptimer.core.LoadingPlugin',
                "ForceLoadAsMod": project.gradle.startParameter.taskNames[0] == "build" || project.gradle.startParameter.taskNames[0] == "curseforge",
                "TweakClass": 'org.spongepowered.asm.launch.MixinTweaker'
        ])
    }
}

curseforge {
    if (System.getenv("CURSEFORGE_TOKEN") != null) {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        project {
            id = "782070"
            changelog = '[Changelog is not currently available]'
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion 'Forge'
            // add supported game versions separated by comma
            addGameVersion "1.12.2"
            mainArtifact(jar) {
                relations {
                    requiredDependency 'mixin-booter'
                }
            }
        }
        options {
            javaVersionAutoDetect = false
        }
    }
}